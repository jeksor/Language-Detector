import com.android.builder.core.BuilderConstants

apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//apply plugin: 'com.getkeepsafe.dexcount'

android {
	compileSdkVersion globalCompileSdkVersion
	buildToolsVersion globalBuildToolsVersion

	defaultConfig {
		applicationId "com.esorokin.lantector"
		minSdkVersion globalMinSdkVersion
		targetSdkVersion globalTargetVersion
		versionName '0.1.0' // Major, Minor, Bugfix
		versionCode 1

		vectorDrawables.useSupportLibrary = true

		buildConfigField "boolean", "IS_TURN_LOGS", buildServerEnable.toBoolean() ? buildServerLogs.toBoolean() : "true"
		manifestPlaceholders = [applicationLabel: "Language Detector"]
	}

	signingConfigs {
		// you must set up an environment var before release signing
		// run: export APP_KEY={password}
		// TODO add your release keystore in /keystore folder
		release {
			storeFile file('../keystore/release.keystore')
			keyAlias 'alias'
			storePassword "$System.env.APP_KEY"
			keyPassword "$System.env.APP_KEY"
		}

		debug {
			storeFile file('../keystore/debug.keystore')
			keyAlias 'androiddebugkey'
			storePassword 'android'
			keyPassword 'android'
		}
	}

	buildTypes {
		prod {
			signingConfig signingConfigs.release
			minifyEnabled buildServerEnable.toBoolean()
			debuggable !buildServerEnable.toBoolean()
			proguardFile getDefaultProguardFile('proguard-android.txt')
			proguardFiles file('../config/proguard').listFiles()

			manifestPlaceholders = [applicationLabelSuffix: ""]
		}

		dev {
			signingConfig signingConfigs.debug
			minifyEnabled false
			debuggable true
			proguardFile getDefaultProguardFile('proguard-android.txt')
			proguardFiles file('../config/proguard').listFiles()

			manifestPlaceholders = [applicationLabelSuffix: " dev"]
			applicationIdSuffix ".dev"
			versionNameSuffix " Dev"
		}
	}

	//allow to use custom build type like {dev, stage, live}
	android.variantFilter { variant ->
		def build = variant.buildType.name
		if (build == BuilderConstants.DEBUG || build == BuilderConstants.RELEASE) {
			variant.setIgnore(true)
		}
	}

	kapt {
		generateStubs = true
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	//kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

	//mvp
	implementation "com.arello-mobile:moxy:$moxy"
	implementation "com.arello-mobile:moxy-app-compat:$moxy"
	kapt "com.arello-mobile:moxy-compiler:$moxy"

	//di
	kapt "com.google.dagger:dagger-compiler:$dagger2"
	implementation "com.google.dagger:dagger:$dagger2"
	compileOnly 'javax.annotation:jsr250-api:1.0'

	//support libraries
	implementation "com.android.support:appcompat-v7:$support"
	implementation "com.android.support:design:$support"
	implementation "com.android.support:recyclerview-v7:$support"

	//ui
	implementation "com.android.support.constraint:constraint-layout:$constraintLayout"

	//network
	implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
	implementation "com.squareup.retrofit2:retrofit:$retrofit"
	implementation("com.squareup.retrofit2:converter-simplexml:$retrofit") {
		exclude group: 'xpp3', module: 'xpp3'
		exclude group: 'stax', module: 'stax-api'
		exclude group: 'stax', module: 'stax'
	}

	//db
	implementation "io.requery:requery:$requery"
	implementation "io.requery:requery-android:$requery"
	implementation "io.requery:requery-kotlin:$requery"
	kapt "io.requery:requery-processor:$requery"

	//rx
	implementation "io.reactivex.rxjava2:rxjava:$rxjava"
	implementation "io.reactivex.rxjava2:rxandroid:$rxjava"

	//navigation
	implementation "com.github.aartikov:Alligator:$alligator"

	//utils
	implementation "com.jakewharton.timber:timber:$timber"
	implementation "com.hannesdorfmann:adapterdelegates3:$adapterDelegates"
}